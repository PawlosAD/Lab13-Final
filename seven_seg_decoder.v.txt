// Controls 7 segment display by decoding 4 bit binary values into corresponding 7 segment patterns for digits 0-F
// Seletcs which value to display (A, B, AplusB, or AminusB) based on active anode line using active low logig for anode selection
module seven_seg_decoder(
    input [3:0] A, //   4 bit value for operand A
    input [3:0] B, // 4 bit value for operand B
    input [3:0] AplusB, // 4 bit result of A + B
    input [3:0] AminusB, // 4 bit result oof A-B
    input [3:0] anode, // 4 bit active low anode selector input
    output reg [6:0] segs // 7 bit output to drive 7 segment display segments
);
    reg [3:0] value_to_display; //Internal register to hold currently selected value for display
    
    // Multiplexer to select which value to send to 7 segment decoder
    // based on active anode input where '0' means active
    always @(*) begin
        case(anode)
            4'b1110: begin
                value_to_display = A; // Anode 0 active - Show A
            end
            4'b1101: begin
                value_to_display = B; // Anode 1 active - Show B
            end
            4'b1011: begin
                value_to_display = AplusB; // Anode 2 active - Show A + B
            end
            4'b0111: begin
                value_to_display = AminusB; // Anode 3 active - Show  A - B
            end
            default: begin
                value_to_display = 4'b0000; // If no valid anode, default to 0
            end
        endcase
    end
    
    // Decode 4-bit value to 7-segment display
    // Segment encoding: {G, F, E, D, C, B, A}
    always @(*) begin
        case(value_to_display)
            //             GFEDCBA
            4'd0: segs = 7'b1000000;  // 0
            4'd1: segs = 7'b1111001;  // 1
            4'd2: segs = 7'b0100100;  // 2
            4'd3: segs = 7'b0110000;  // 3
            4'd4: segs = 7'b0011001;  // 4
            4'd5: segs = 7'b0010010;  // 5
            4'd6: segs = 7'b0000010;  // 6
            4'd7: segs = 7'b1111000;  // 7
            4'd8: segs = 7'b0000000;  // 8
            4'd9: segs = 7'b0010000;  // 9
            4'd10: segs = 7'b0001000; // A
            4'd11: segs = 7'b0000011; // b
            4'd12: segs = 7'b1000110; // C
            4'd13: segs = 7'b0100001; // d
            4'd14: segs = 7'b0000110; // E
            4'd15: segs = 7'b0001110; // F
            default: segs = 7'b1111111; // All off
        endcase
    end
endmodule

